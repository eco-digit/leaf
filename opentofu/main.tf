terraform {
  required_providers {
    openstack = { source = "terraform-provider-openstack/openstack" }
    random    = { source = "hashicorp/random" }
    local     = { source = "hashicorp/local" }
    null      = { source = "hashicorp/null" }
  }
}

provider "openstack" {
  cloud = var.OS_CLOUD
}

resource "random_password" "random_passwd" {
  length      = var.RANDOM_PASSWD_LENGTH
  min_lower   = 1
  min_numeric = 1
  min_special = 1
  min_upper   = 1
  special     = true
}

### https://registry.terraform.io/providers/terraform-provider-openstack/openstack/latest/docs/resources/compute_keypair_v2
resource "openstack_compute_keypair_v2" "leaf_keypair" {
  name       = "leaf_keypair"
  public_key = file(var.KEYPAIR_PATH)
}

### https://registry.terraform.io/providers/terraform-provider-openstack/openstack/latest/docs/data-sources/networking_network_v2
data "openstack_networking_network_v2" "public_network" {
  network_id = var.PUBLIC_NETWORK_ID
  external   = true
}

### https://registry.terraform.io/providers/terraform-provider-openstack/openstack/latest/docs/data-sources/networking_subnet_ids_v2
data "openstack_networking_subnet_ids_v2" "public_network_subnet4" {
  network_id = var.PUBLIC_NETWORK_ID
  ip_version = 4
}

data "openstack_networking_subnet_ids_v2" "public_network_subnet6" {
  network_id = var.PUBLIC_NETWORK_ID
  ip_version = 6
}

### https://registry.terraform.io/providers/terraform-provider-openstack/openstack/latest/docs/data-sources/networking_secgroup_v2
data "openstack_networking_secgroup_v2" "default_network_secgroup" {
  name = "default"
}

### https://registry.terraform.io/providers/terraform-provider-openstack/openstack/latest/docs/resources/networking_floatingip_v2
resource "openstack_networking_floatingip_v2" "leaf_floatingip4" {
  pool       = data.openstack_networking_network_v2.public_network.name
  subnet_ids = data.openstack_networking_subnet_ids_v2.public_network_subnet4.ids
  depends_on = [
    openstack_networking_router_interface_v2.leaf_router_interface4
  ]
}

# resource "openstack_networking_floatingip_v2" "leaf_floatingip6" {
#   pool = data.openstack_networking_network_v2.public_network.name
#   subnet_id = data.openstack_networking_subnet_ids_v2.public_network_subnet6.id
#   depends_on = [
#     openstack_networking_router_interface_v2.leaf_router_interface6
#   ]
# }

### https://registry.terraform.io/providers/terraform-provider-openstack/openstack/latest/docs/resources/networking_network_v2
resource "openstack_networking_network_v2" "leaf_network" {
  name                  = "leaf_network"
  admin_state_up        = true
  port_security_enabled = true
}

### https://registry.terraform.io/providers/terraform-provider-openstack/openstack/latest/docs/resources/networking_subnet_v2
resource "openstack_networking_subnet_v2" "leaf_network_subnet4" {
  name            = "leaf_network_subnet4"
  network_id      = openstack_networking_network_v2.leaf_network.id
  cidr            = var.NETWORK_SUBNET4_CIDR
  ip_version      = 4
  dns_nameservers = var.NETWORK_SUBNET4_NAMESERVERS
}

resource "openstack_networking_subnet_v2" "leaf_network_subnet6" {
  name            = "leaf_network_subnet6"
  network_id      = openstack_networking_network_v2.leaf_network.id
  cidr            = var.NETWORK_SUBNET6_CIDR
  ip_version      = 6
  dns_nameservers = var.NETWORK_SUBNET6_NAMESERVERS
}

### https://registry.terraform.io/providers/terraform-provider-openstack/openstack/latest/docs/resources/networking_secgroup_v2
resource "openstack_networking_secgroup_v2" "leaf_network_secgroup" {
  name                 = "leaf_network_secgroup"
  delete_default_rules = true
}

### https://registry.terraform.io/providers/terraform-provider-openstack/openstack/latest/docs/resources/networking_secgroup_rule_v2
resource "openstack_networking_secgroup_rule_v2" "leaf_network_secgroup_rules4i" {
  description       = "Allow SSH ingress via IPv4"
  direction         = "ingress"
  ethertype         = "IPv4"
  port_range_max    = 22
  port_range_min    = 22
  protocol          = "tcp"
  remote_ip_prefix  = "0.0.0.0/0"
  security_group_id = openstack_networking_secgroup_v2.leaf_network_secgroup.id
}

resource "openstack_networking_secgroup_rule_v2" "leaf_network_secgroup_rules4e" {
  description       = "Allow egress via IPv4"
  direction         = "egress"
  ethertype         = "IPv4"
  remote_ip_prefix  = "0.0.0.0/0"
  security_group_id = openstack_networking_secgroup_v2.leaf_network_secgroup.id
}

resource "openstack_networking_secgroup_rule_v2" "leaf_network_secgroup_rules6i" {
  description       = "Allow SSH ingress via IPv6"
  direction         = "ingress"
  ethertype         = "IPv6"
  port_range_max    = 22
  port_range_min    = 22
  protocol          = "tcp"
  remote_ip_prefix  = "::/0"
  security_group_id = openstack_networking_secgroup_v2.leaf_network_secgroup.id
}

resource "openstack_networking_secgroup_rule_v2" "leaf_network_secgroup_rules6e" {
  description       = "Allow egress via IPv6"
  direction         = "egress"
  ethertype         = "IPv6"
  remote_ip_prefix  = "::/0"
  security_group_id = openstack_networking_secgroup_v2.leaf_network_secgroup.id
}

### https://registry.terraform.io/providers/terraform-provider-openstack/openstack/latest/docs/resources/networking_port_v2
resource "openstack_networking_port_v2" "leaf_network_port4" {
  admin_state_up = true
  name           = "leaf_network_port4"
  network_id     = openstack_networking_network_v2.leaf_network.id
  # security_group_ids = [data.openstack_networking_secgroup_v2.default_network_secgroup.id]
  security_group_ids = [openstack_networking_secgroup_v2.leaf_network_secgroup.id]
  depends_on         = [openstack_networking_subnet_v2.leaf_network_subnet4]
  fixed_ip {
    ip_address = openstack_networking_floatingip_v2.leaf_floatingip4.fixed_ip
    subnet_id  = openstack_networking_subnet_v2.leaf_network_subnet4.id
  }
}

resource "openstack_networking_port_v2" "leaf_network_port6" {
  admin_state_up = true
  name           = "leaf_network_port6"
  network_id     = openstack_networking_network_v2.leaf_network.id
  # security_group_ids = [data.openstack_networking_secgroup_v2.default_network_secgroup.id]
  security_group_ids = [openstack_networking_secgroup_v2.leaf_network_secgroup.id]
  depends_on         = [openstack_networking_subnet_v2.leaf_network_subnet6]
  fixed_ip {
    # ip_address = openstack_networking_floatingip_v2.leaf_floatingip6.fixed_ip
    subnet_id = openstack_networking_subnet_v2.leaf_network_subnet6.id
  }
}

### https://registry.terraform.io/providers/terraform-provider-openstack/openstack/latest/docs/resources/networking_router_v2
resource "openstack_networking_router_v2" "leaf_router" {
  name                = "leaf_router"
  external_network_id = var.PUBLIC_NETWORK_ID
  depends_on = [
    openstack_networking_subnet_v2.leaf_network_subnet4,
    openstack_networking_subnet_v2.leaf_network_subnet6
  ]
}

### https://registry.terraform.io/providers/terraform-provider-openstack/openstack/latest/docs/resources/networking_router_interface_v2
resource "openstack_networking_router_interface_v2" "leaf_router_interface4" {
  router_id = openstack_networking_router_v2.leaf_router.id
  subnet_id = openstack_networking_subnet_v2.leaf_network_subnet4.id
}

resource "openstack_networking_router_interface_v2" "leaf_router_interface6" {
  router_id = openstack_networking_router_v2.leaf_router.id
  subnet_id = openstack_networking_subnet_v2.leaf_network_subnet6.id
}

### https://registry.terraform.io/providers/terraform-provider-openstack/openstack/latest/docs/resources/networking_floatingip_associate_v2
resource "openstack_networking_floatingip_associate_v2" "leaf_floatingip4_associate" {
  floating_ip = openstack_networking_floatingip_v2.leaf_floatingip4.address
  port_id     = openstack_networking_port_v2.leaf_network_port4.id

  depends_on = [
    openstack_networking_router_interface_v2.leaf_router_interface4,
    openstack_networking_router_v2.leaf_router,
    openstack_networking_subnet_v2.leaf_network_subnet4
  ]
}

### https://registry.terraform.io/providers/terraform-provider-openstack/openstack/latest/docs/resources/compute_instance_v2
resource "openstack_compute_instance_v2" "leaf_instance" {
  admin_pass  = random_password.random_passwd.result
  flavor_name = var.INSTANCE_FLAVOR_NAME
  image_name  = var.INSTANCE_IMAGE_NAME
  key_pair    = openstack_compute_keypair_v2.leaf_keypair.name
  name        = "leaf_instance"

  network {
    name = openstack_networking_network_v2.leaf_network.name
    uuid = openstack_networking_network_v2.leaf_network.id
    port = openstack_networking_port_v2.leaf_network_port4.id
  }
  network {
    name = openstack_networking_network_v2.leaf_network.name
    uuid = openstack_networking_network_v2.leaf_network.id
    port = openstack_networking_port_v2.leaf_network_port6.id
  }

  connection {
    type     = "ssh"
    user     = var.INSTANCE_USER_NAME
    host     = openstack_networking_floatingip_v2.leaf_floatingip4.address
    agent    = var.SSH_AGENT_ENABLE
    password = random_password.random_passwd.result
  }

  lifecycle {
    ignore_changes = [admin_pass]
  }

  provisioner "local-exec" {
    command = "ssh-keygen -R \"${openstack_networking_floatingip_v2.leaf_floatingip4.address}\" || true"
  }
}
################################################################################

resource "local_file" "leaf_ansible_inventory" {
  content         = "leaf_instance ansible_host=${openstack_networking_floatingip_v2.leaf_floatingip4.address} ansible_user=${var.INSTANCE_USER_NAME}\n"
  filename        = "../ansible/inventory_${var.OS_CLOUD}.ini"
  file_permission = "0640"
}
